plugins {
    id 'java'
    id 'idea'
}

group = 'com.yourname'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Cucumber core
    testImplementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit:7.15.0'
    testImplementation 'io.cucumber:cucumber-core:7.15.0'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.15.0'

    // Selenium (untuk Web UI)
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.21.0'

    // Rest-Assured (untuk API testing)
    testImplementation 'io.rest-assured:rest-assured:5.4.0'

    // JSON validation
    testImplementation 'org.hamcrest:hamcrest:2.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'

    // JUnit
    testImplementation 'junit:junit:4.13.2'

    // Logging
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'

    implementation 'io.github.bonigarcia:webdrivermanager:5.9.2'

    implementation 'org.json:json:20240303'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
}

tasks.register('apiTest', Test) {
    useJUnit()
    systemProperty "cucumber.filter.tags", "@api"
    testLogging {
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen { false }
}

tasks.register('webTest', Test) {
    useJUnit()
    systemProperty "cucumber.filter.tags", "@web"
    testLogging {
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen { false }
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

// Task untuk generate report HTML dan JSON
tasks.register('cucumberReport', JavaExec) {
    mainClass = 'io.cucumber.core.cli.Main'
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:build/reports/cucumber-report.html',
            '--plugin', 'json:build/reports/cucumber-report.json',
            '--glue', 'steps',
            'src/test/resources/features'
    ]
}
